model Todo {
    fields {
        complete Boolean @default(false)
        title Text
        description Text?
        completedAt Timestamp?
    }

    operations {
        // Operations are built in actions in the structre of
        // <get | list | delete> <operation name>(<where inputs>)
        // OR
        // <create | update> <operation name>(<where inputs>) with (<value inputs>)
        //
        // Additional attributes such as @set, @where and @validate can be adding to the body of the operation
        //
        create createTodo() with (title)
        get todo(id)
        list allTodos(complete?)
        list completedTodos() {
            @where(todo.complete == true)
        }
        update updateTodo(id) with (title?, description?)
        delete deleteTodo(id)
    }

    functions {
        // Functions are custom actions
        // Follow a similar structure to operations but trigger custom code
        // See this funcitons in /functions/setCompletion.ts
        update setCompletion(id) with (complete)
    }
}

api Web {
    @graphql

    models {
        Todo
    }
}

api Console {
    @rpc

    models {
        Todo
    }
}
